module ribbon.add

require console as console
require file as f
require `org.yaml.snakeyaml.Yaml`

quit(msg) ->
  def: 'Usage: ribbon add <dependency> \n'
  if msg == Nothing then print(def) else print(msg)
  exit(1)

validate(args) ->
  yaml_exists = new java.io.File('ribbon.yml').exists()
  if args.size() < 2 then quit(Nothing) else Nothing
  if yaml_exists then Nothing else quit('not a valid ribbon project (have you run ribbon init?) \n')

write_yaml(dep) ->
  yaml = new Yaml()
  config = yaml.load(f.read('ribbon.yml'))
  print("   update ribbon.yml")
  config.deps.add(dep)
  f.write('ribbon.yml', yaml.dump(config))

check_dep(dep) ->
  yaml =  new Yaml()
  config = yaml.load(f.read('ribbon.yml'))
  if config.deps.contains(dep) then finish_early() else add_dep(dep)
  where
    finish_early ->
      print('   (exists)')

add_dep(dep) ->
  `org.looplang.ribbon.bootstrap.Bootstrap`.fetchDependency(dep)
  write_yaml(dep)

main(args) ->
  validate(args)
  console.write("      add @{args[1]}   ")
  check_dep(args[1])
