module ribbon.init

require file as f
require `org.yaml.snakeyaml.Yaml`

quit() ->
  print('Usage: ribbon init <project_name> \n'),
  exit(1)

validate(args) ->
  if args.size() < 2 then quit() else Nothing

write_yaml(args) ->
  print("    write ribbon.yml"),
  f.write('ribbon.yml', project.toString())
  where
    yaml:    new Yaml()
    project: yaml.dump({ @name: args[1], @deps: [] })

create_dir(dir) ->
  print("   create @{dir}"),
  new java.io.File(dir).mkdirs()

create_dirs() ->
  'app'.create_dir(),
  'views'.create_dir(),
  'assets/images'.create_dir(),
  'assets/scripts'.create_dir(),
  'assets/css'.create_dir()

add_dep(dep) ->
  print_fragment("  add_dep @{dep}"),
  `org.looplang.ribbon.bootstrap.Bootstrap`.fetchDependency(dep)

add_web_deps() ->
  add_dep('org.looplang.ribbon:ribbon-web:1.0')

main(args) ->
  validate(args),
  print("     init @{args[1]}..."),
  create_dirs(),
  write_yaml(args),
  add_web_deps()